apply plugin: 'com.android.application'

repositories {
    /**
     *  Maven repository for the Tapkey SDK.
     */
    maven { url "https://maven.tapkey.com" }

    /**
     * Maven repository for the WITTE mobile library.
     */
    maven {
        url "https://dl.bintray.com/witte-digital/maven/"
    }
}

def getWitteCustomerId() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("WITTE_CUSTOMER_ID")
}

def getWitteSdkKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("WITTE_SDK_KEY")
}

def getWitteSubscriptionKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("WITTE_SUBSCRIPTION_KEY")
}
android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "digital.witte.wittemobilelibrary.sample"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "WITTE_CUSTOMER_ID", "\"" + getWitteCustomerId() + "\""
        buildConfigField "String", "WITTE_SDK_KEY", "\"" + getWitteSdkKey() + "\""
        buildConfigField "String", "WITTE_SUBSCRIPTION_KEY", "\"" + getWitteSubscriptionKey() + "\""
    }

    /**
     * Tapkey SDK requires Java 8 source code compatibility
     */
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        release {
            /**
             * Apps using the Tapkey SDK  need to be obfuscated for publishing.
             */
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Standard dependencies
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha04'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0'

    // WITTE Mobile Library
    implementation 'digital.witte:witte-mobile-library:1.0.0'

    // Tapkey SDK
    api 'com.tapkey.android:Tapkey.MobileLib:1.15.12.0'

    // Test related dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-beta01'
}
